 r←host GetPage page;QRY;msg;SOCK;P;BUFFER
⍝ Get a page from a host

 QRY←'GET ',page,' HTTP/1.1'
 QRY,←⎕AV[4 3],'Host: ',host,⎕AV[4 3 4 3]

 P←('RemoteAddrName'host)('RemotePort' 80)('Event'('TCPConnect' 'TCPRecv' 'TCPClose' 'TCPError')1)
 'SOCK'⎕WC(⊂'TCPSocket'),P
 ⍝ ⎕NQ'SOCK' 'TCPSend'QRY
 BUFFER←''

 :Repeat
     :If 2≤⍴msg←⎕DQ'SOCK'
         :Select 2⊃msg
         :Case 'TCPConnect'
             ⎕NQ'SOCK' 'TCPSend'QRY

         :Case 'TCPRecv'
             BUFFER,←3⊃msg
             :If ∨/'</html>'⍷BUFFER
             :OrIf ∨/'</HTML>'⍷BUFFER
                 (⊃msg)##.⎕WS'TargetState' 'Closed'
                 r←0 BUFFER
             :EndIf

         :Case 'TCPClose'
             r←1 'Socket closed unexpectedly'BUFFER

         :Case 'TCPError'
             r←1(3⊃msg)
         :EndSelect

     :Else
         r←1 'Unknown error' ⋄ →0 ⍝ Error
     :EndIf

 :Until 2=⎕NC'r'
