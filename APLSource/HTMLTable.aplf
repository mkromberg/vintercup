 HTML←{ALIGN}HTMLTable RCV;ROWTITLES;COLTITLES;VALUES;CRLF;TAB;⎕ML;class
 ⍝ LEFT|RIGHT|CENTER HTMLTable RowTitles ColTitles Data

 ⎕ML←2
 class←'class="vctab"'
 CRLF←⎕UCS 13 10
 :If 0=⎕NC'ALIGN'
     ALIGN←'center'
 :EndIf
 ROWTITLES COLTITLES VALUES←RCV
 :If 1=≡ALIGN
     VALUES←(⊂'<td ',class,' align="',ALIGN,'" nowrap>'),¨VALUES,¨(⊂'</td>',CRLF)
 :Else
     ALIGN←(⍴VALUES)⍴ALIGN
     VALUES←(⊂'<td ',class,' align="'),¨ALIGN,¨(⊂'" nowrap>'),¨VALUES,¨(⊂'</td>',CRLF)
 :EndIf
 HTML←'<table cellspacing=1 cellpadding=1 >',CRLF
 HTML,←'<tr>',CRLF
 :If ''≢COLTITLES
 :AndIf ''≢ROWTITLES
     HTML,←'<th ',class,' align="left">&nbsp;</th>',CRLF
 :EndIf
 :If ''≢COLTITLES
     COLTITLES←(⊂'<th ',class,' align="center" nowrap>'),¨COLTITLES,¨(⊂'</th>',CRLF)
     HTML,←∊COLTITLES
 :EndIf
 HTML,←'</tr>',CRLF
 :If ROWTITLES≡''
     HTML,←∊(⊂'<tr>',CRLF),VALUES,⊂'</tr>',CRLF
 :Else
     ROWTITLES←(⊂'<th ',class,' align="left" nowrap>'),¨ROWTITLES,¨(⊂'</th>',CRLF)
     HTML,←∊(⊂'<tr>',CRLF),ROWTITLES,VALUES,⊂'</tr>',CRLF
 :EndIf
 HTML,←'</table>',CRLF
